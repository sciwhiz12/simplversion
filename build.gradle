plugins {
    id 'java-gradle-plugin'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'maven-publish'
}

group = 'tk.sciwhiz12.gradle'
version = '0.1.0'

license {
    header(project.file('LICENSE.txt'))
}

repositories {
    mavenCentral()
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withJavadocJar()
}

dependencies {
    //noinspection GradlePackageUpdate We can't upgrade to 6.x because that's Java 11
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.13.1.202206130422-r'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

gradlePlugin {
    plugins {
        simplversion {
            displayName = 'simplversion'
            description = 'A simple Git tag-based versioning plugin'
            id = 'tk.sciwhiz12.gradle.simplversion'
            implementationClass = 'tk.sciwhiz12.gradle.simplversion.VersionPlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register('functionalTest', Test) {
    group 'verification'
    description 'Runs the functional test suite.'

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    dependsOn(tasks.named('functionalTest'))
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(Jar).configureEach {
    includeEmptyDirs false
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

publishing {
    publications {
        pluginMaven(MavenPublication) { // Auto-defined by the Gradle plugin-dev plugin
            pom {
                name = 'simplversion'
                description = 'A simple Git tag-based versioning plugin'
                url = 'https://github.com/sciwhiz12/simplversion'
                inceptionYear = '2022'
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/sciwhiz12/simplversion/issues'
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        name = 'sciwhiz12'
                        email = 'sciwhiz12@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        if (project.hasProperty('inquisitionReleasesPassword'))
            maven {
                name 'inquisitionReleases'
                url 'https://maven.moddinginquisition.org/releases'
                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }

        if (project.hasProperty('inquisitionSnapshotsPassword'))
            maven {
                name 'inquisitionSnapshots'
                url 'https://maven.moddinginquisition.org/snapshots'
                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }
    }
}
