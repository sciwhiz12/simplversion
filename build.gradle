plugins {
    id 'java-gradle-plugin'
    id 'org.cadixdev.licenser' version '0.6.1'
}

group = 'tk.sciwhiz12.gradle'
version = '0.1.0-SNAPSHOT'

license {
    header(project.file('LICENSE.txt'))
}

repositories {
    mavenCentral()
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withJavadocJar()
}

dependencies {
    //noinspection GradlePackageUpdate We can't upgrade to 6.x because that's Java 11
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.13.1.202206130422-r'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

gradlePlugin {
    plugins {
        greeting {
            id = 'tk.sciwhiz12.gradle.simplversion'
            implementationClass = 'tk.sciwhiz12.gradle.simplversion.VersionPlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register('functionalTest', Test) {
    group 'verification'
    description 'Runs the functional test suite.'

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    dependsOn(tasks.named('functionalTest'))
}

tasks.named('test') {
    useJUnitPlatform()
}
